load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("//tools:index.bzl", "generate_ide_tsconfig_json", "generate_src_file", "package_json_test", "ts_compile_node")
load("//tools:vitest.bzl", "vitest")

npm_link_all_packages()

PACKAGE_NAME = "eslint-plugin-formatjs"

# Copy emoji-data.txt from external repository
copy_file(
    name = "emoji-data-txt",
    src = "@emoji-data//file",
    out = "emoji-data.txt",
)

# Generate emoji version data from Unicode emoji-data.txt
generate_src_file(
    name = "emoji-data-generated",
    src = "emoji-data.generated.ts",
    args = [
        "--emojiDataPath",
        "$(rootpath :emoji-data-txt)",
    ],
    data = [
        ":emoji-data-txt",
    ],
    entry_point = "scripts/generate-emoji-data.ts",
)

npm_package(
    name = "pkg",
    srcs = [
        "LICENSE.md",
        "README.md",
        "package.json",
        ":dist",
    ],
    package = PACKAGE_NAME,
    visibility = ["//visibility:public"],
)

SRCS = glob(["rules/*.ts"]) + [
    "index.ts",
    "util.ts",
    "messages.ts",
    "context-compat.ts",
    "emoji-utils.ts",
    "emoji-data.generated.ts",
    "package.json",
]

SRC_DEPS = [
    ":node_modules/@formatjs/icu-messageformat-parser",
    ":node_modules/@formatjs/ts-transformer",
    ":node_modules/@types/picomatch",
    ":node_modules/magic-string",
    ":node_modules/picomatch",
    "//:node_modules/@types/node",
    "//:node_modules/@typescript-eslint/utils",
    "//:node_modules/@unicode/unicode-17.0.0",
    "//:node_modules/eslint",
    "//:node_modules/typescript",
]

ts_compile_node(
    name = "dist",
    srcs = [":srcs"],
    deps = SRC_DEPS,
)

TESTS_BASE_SRCS = [":srcs"] + glob(
    [
        "tests/*.ts",
    ],
    exclude = ["tests/*.test.ts"],
)

TEST_FILES = glob([
    "tests/*.test.ts",
])

vitest(
    name = "unit_test",
    srcs = TESTS_BASE_SRCS + TEST_FILES,
    deps = SRC_DEPS + [
        "//:node_modules/@types/eslint-scope",
        "//:node_modules/@typescript-eslint/parser",
        "//:node_modules/@typescript-eslint/rule-tester",
        "//:node_modules/vue-eslint-parser",
    ],
)

# Generate tsconfig.json with correct extends path
generate_ide_tsconfig_json()

package_json_test(
    name = "package_json_test",
    deps = SRC_DEPS,
)

copy_to_bin(
    name = "srcs",
    srcs = SRCS,
)
