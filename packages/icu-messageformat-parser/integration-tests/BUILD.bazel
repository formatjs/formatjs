load("@npm//:defs.bzl", "npm_link_all_packages")
load("@rules_rust//rust:defs.bzl", "rust_test")
load("//tools:index.bzl", "generate_src_file")
load("//tools:vitest.bzl", "vitest")

npm_link_all_packages()

TEST_CASES = glob(["test_cases/*"])

# Generate a file listing all test case filenames
genrule(
    name = "generate_test_list",
    srcs = TEST_CASES,
    outs = ["test_list.txt"],
    cmd = "echo '$(SRCS)' | tr ' ' '\\n' | xargs -n1 basename > $@",
)

# Single Rust test that runs all integration test cases
rust_test(
    name = "integration_tests",
    size = "small",
    srcs = ["integration_tests.rs"],
    data = TEST_CASES + [":test_list.txt"],
    deps = [
        "//rust/icu_messageformat_parser",
        "@crates//:pretty_assertions",
        "@crates//:serde_json",
        "@rules_rust//rust/runfiles",
    ],
)

# [ts_run_binary(
#     name = "gen_parser_test_%s" % f.split("/")[-1],
#     outs = ["%s.tmp" % f.split("/")[-1]],
#     args = [
#         "--input",
#         f
#     ],
#     data = [
#         ":node_modules/@formatjs/icu-messageformat-parser",
#         "//:node_modules/lodash",
#         f
#     ],
#     entry_point = "run-parser.ts",
# ) for f in TEST_CASES]

[generate_src_file(
    name = "gen_parser_test_%s" % f.split("/")[-1],
    src = f,
    args = [
        "--input %s/%s" % (
            package_name(),
            f,
        ),
    ],
    data = [
        ":node_modules/@formatjs/icu-messageformat-parser",
        "//:node_modules/lodash-es",
        f,
    ],
    entry_point = "run-parser.ts",
) for f in TEST_CASES]

# WASM integration test
vitest(
    name = "wasm_integration_test",
    srcs = ["wasm-integration.test.ts"],
    fixtures = TEST_CASES + [
        "//packages/icu-messageformat-parser-wasm:pkg",
    ],
    deps = [
        ":node_modules/@formatjs/icu-messageformat-parser-wasm",
    ],
)
