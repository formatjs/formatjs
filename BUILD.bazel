load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_rules_js//npm:defs.bzl", "npm_link_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:karma/package_json.bzl", karma_bin = "bin")
load("@npm//:oxfmt/package_json.bzl", oxfmt = "bin")
load("@rules_multirun//:defs.bzl", "multirun")
load("//tools:tsconfig.bzl", "BASE_TSCONFIG")

npm_link_all_packages()

exports_files(
    [
        "karma.conf.js",
        "karma.conf-ci.js",
    ],
    visibility = ["//:__subpackages__"],
)

genrule(
    name = "pnpm_workspace_config",
    srcs = [],
    outs = ["pnpm-workspace.yaml"],
    cmd = "echo 'packages:\n\
  - 'packages/*' \
' > $@",
)

PACKAGES_TO_DIST = [
    "//packages/babel-plugin-formatjs",
    "//packages/cli-lib",
    "//packages/cli",
    "//packages/ecma376",
    "//packages/ecma402-abstract",
    "//packages/eslint-plugin-formatjs",
    "//packages/fast-memoize",
    "//packages/icu-messageformat-parser",
    "//packages/icu-skeleton-parser",
    "//packages/intl-datetimeformat",
    "//packages/intl-displaynames",
    "//packages/intl-durationformat",
    "//packages/intl-enumerator",
    "//packages/intl-getcanonicallocales",
    "//packages/intl-listformat",
    "//packages/intl-locale",
    "//packages/intl-localematcher",
    "//packages/intl-messageformat",
    "//packages/intl-numberformat",
    "//packages/intl-pluralrules",
    "//packages/intl-relativetimeformat",
    "//packages/intl-segmenter",
    "//packages/intl",
    "//packages/react-intl",
    "//packages/ts-transformer",
    "//packages/utils",
    "//packages/vue-intl",
]

PACKAGE_DIRNAMES = [p.split("packages/")[1] for p in PACKAGES_TO_DIST]

# pnpm_bin.pnpm(
#    name="deploy",
#    srcs=[":dist"],
#    out_dirs=["deploy_result"],
#    chdir="$(rootpath :dist)",
#    args=["deploy", "--filter", "@formatjs/cli-lib", "../$(@D)"]
# )

copy_to_directory(
    name = "dist",
    srcs = [
        # to verify, run verdaccio at port 4000 and enable this
        ":pnpm_workspace_config",
        "package.json",
        "pnpm-lock.yaml",
    ] + ["%s:pkg" % pkg for pkg in PACKAGES_TO_DIST] + glob(["patches/*"]),
    out = "formatjs_dist",
    replace_prefixes = {k: v for k, v in [(
        "packages/%s/pkg" % p,
        "packages/%s" % p,
    ) for p in PACKAGE_DIRNAMES]},
)

# We run this centrally so it doesn't spawn
# multiple browser sessions which overwhelms SauceLabs
KARMA_TESTS = [
    # TODO: fix this
    "//packages/icu-messageformat-parser:bundled-karma-tests",
    "//packages/intl-displaynames:bundled-karma-tests",
    "//packages/intl-getcanonicallocales:bundled-karma-tests",
    "//packages/intl-listformat:bundled-karma-tests",
    "//packages/intl-pluralrules:bundled-karma-tests",
    "//packages/intl-relativetimeformat:bundled-karma-tests",
]

karma_bin.karma_test(
    name = "karma",
    args = [
        "start",
        "$(rootpath //:karma.conf.js)",
        "--no-single-run",
    ],
    # configuration_env_vars = [
    #     "SAUCE_USERNAME",
    #     "SAUCE_ACCESS_KEY",
    # ],
    data = [
        "//:karma.conf.js",
        "//:node_modules/karma-chrome-launcher",
        "//:node_modules/karma-jasmine",
        "//:node_modules/karma-jasmine-matchers",
        "//:node_modules/karma-sauce-launcher",
    ] + KARMA_TESTS,
    tags = ["manual"],
)

karma_bin.karma_test(
    name = "karma-ci",
    args = [
        "start",
        "$(rootpath //:karma.conf.js)",
        "--browsers",
        "sl_edge,sl_chrome,sl_firefox,sl_ie_11,sl_safari",
    ],
    # configuration_env_vars = [
    #     "SAUCE_USERNAME",
    #     "SAUCE_ACCESS_KEY",
    #     "GITHUB_RUN_ID",
    # ],
    data = [
        "//:karma.conf.js",
        "//:node_modules/karma-chrome-launcher",
        "//:node_modules/karma-jasmine",
        "//:node_modules/karma-jasmine-matchers",
        "//:node_modules/karma-sauce-launcher",
    ] + KARMA_TESTS,
    tags = ["manual"],
)

multirun(
    name = "generated-test-files",
    testonly = True,
    commands = [
        "//packages/intl-datetimeformat:test262-main",
        "//packages/intl-datetimeformat:generated-test-files",
        "//packages/intl-displaynames:test262-main",
        "//packages/intl-displaynames:generated-test-files",
        "//packages/intl-listformat:test262-main",
        "//packages/intl-listformat:generated-test-files",
        "//packages/intl-numberformat:test262-main",
        "//packages/intl-numberformat:generated-test-files",
        "//packages/intl-pluralrules:test262-main",
        "//packages/intl-pluralrules:generated-test-files",
        "//packages/intl-relativetimeformat:test262-main",
        "//packages/intl-relativetimeformat:generated-test-files",
        "//packages/intl-segmenter:test262-main",
        "//packages/intl-segmenter:generated-files",
        "//packages/intl-getcanonicallocales:aliases",
    ],
)

multirun(
    name = "generated-files",
    testonly = True,
    commands = [
        "//packages/intl-datetimeformat:supported-locales",
        "//packages/intl-displaynames:supported-locales",
        "//packages/intl-listformat:supported-locales",
        "//packages/intl-numberformat:supported-locales",
        "//packages/intl-pluralrules:supported-locales",
        "//packages/intl-relativetimeformat:supported-locales",
        "//packages/intl-segmenter:generated-files",
    ],
)

multirun(
    name = "cldr-gen",
    testonly = True,
    jobs = 0,  # Run all targets in parallel
    commands = [
        "//packages/icu-messageformat-parser:time-data",
        "//packages/intl-datetimeformat:all-tz",
        "//packages/intl-datetimeformat:links",
        "//packages/intl-datetimeformat:supported-locales",
        "//packages/intl-datetimeformat:test262-main",
        "//packages/intl-displaynames:supported-locales",
        "//packages/intl-displaynames:test262-main",
        "//packages/intl-durationformat:numbering-systems",
        "//packages/intl-durationformat:time-separators",
        "//packages/intl-enumerator:calendars",
        "//packages/intl-enumerator:collations",
        "//packages/intl-enumerator:currencies",
        "//packages/intl-enumerator:numbering-systems",
        "//packages/intl-enumerator:timezones",
        "//packages/intl-enumerator:units",
        "//packages/intl-getcanonicallocales:aliases",
        "//packages/intl-getcanonicallocales:likelySubtags",
        "//packages/intl-listformat:supported-locales",
        "//packages/intl-listformat:test262-main",
        "//packages/intl-locale:calendars",
        "//packages/intl-locale:character-orders",
        "//packages/intl-locale:hour-cycles",
        "//packages/intl-locale:numbering-systems",
        "//packages/intl-locale:timezones",
        "//packages/intl-locale:week-data",
        "//packages/intl-numberformat:currency-digits",
        "//packages/intl-numberformat:numbering-systems",
        "//packages/intl-numberformat:supported-locales",
        "//packages/intl-numberformat:test262-main",
        "//packages/intl-pluralrules:supported-locales",
        "//packages/intl-pluralrules:test262-main",
        "//packages/intl-relativetimeformat:supported-locales",
        "//packages/intl-relativetimeformat:test262-main",
        "//packages/intl-segmenter:generate-cldr-segmentation-rules",
        "//packages/intl-segmenter:test262-main",
        "//packages/utils:default-currency",
        "//packages/utils:default-locale",
    ],
)

copy_to_bin(
    name = "tsconfig",
    srcs = ["tsconfig.json"],
    visibility = ["//visibility:public"],
)

# Generate tsconfig.json from BASE_TSCONFIG in tools/tsconfig.bzl
genrule(
    name = "tsconfig_generated",
    outs = ["tsconfig.generated.json"],
    cmd = "echo '%s' > $@" % json.encode_indent(
        BASE_TSCONFIG,
        indent = "  ",
    ),
)

# Format the generated tsconfig.json with oxfmt
# Use genrule with proper BAZEL_BINDIR set, and include oxfmt's config in srcs
genrule(
    name = "tsconfig_formatted",
    srcs = [
        ":tsconfig_generated",
        ":oxfmtrc",
    ],
    outs = ["tsconfig.formatted.json"],
    cmd = "cat $(location :tsconfig_generated) | $(location //:oxfmt) --stdin-filepath=$(location :tsconfig_generated) > $@",
    tools = ["//:oxfmt"],
)

# Ensure tsconfig.json stays in sync with BASE_TSCONFIG
write_source_files(
    name = "tsconfig_sync",
    files = {
        "tsconfig.json": ":tsconfig_formatted",
    },
)

# Symlink some workspaces to the root workspace, so scripts like benchmarks
# can easily depend on the distributable package via target `//:node_modules/<package_name>`.
npm_link_package(
    name = "node_modules/@formatjs/intl-pluralrules",
    src = "//packages/intl-pluralrules",
)

npm_link_package(
    name = "node_modules/@formatjs/intl-localematcher",
    src = "//packages/intl-localematcher",
)

copy_to_bin(
    name = "oxfmtrc",
    srcs = [".oxfmtrc.json"],
    visibility = ["//visibility:public"],
)

oxfmt.oxfmt_binary(
    name = "oxfmt",
    data = [
        ":oxfmtrc",
    ],
    env = {"BAZEL_BINDIR": "."},
    fixed_args = [
        "--config=\"$$JS_BINARY__RUNFILES\"/$(rlocationpath //:oxfmtrc)",
    ],
    visibility = ["//visibility:public"],
)
